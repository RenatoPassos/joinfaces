description = 'OpenWebBeans Spring Boot Starter'
tasks.named("jar", Jar) {
    manifest.attributes('Automatic-Module-Name': 'joinfaces.starter.openwebbeans')
}

dependencies {
    api project(':joinfaces-autoconfigure')

    api 'org.apache.openwebbeans:openwebbeans-web'
    implementation 'org.apache.openwebbeans:openwebbeans-el22'
    implementation 'org.apache.openwebbeans:openwebbeans-jsf'

    runtimeOnly('jakarta.enterprise:jakarta.enterprise.cdi-api') {
        exclude group: 'jakarta.el', module: 'jakarta.el-api'
        exclude group: 'jakarta.ejb', module: 'jakarta.ejb-api'
    }

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
}

configurations {
    apiElements {
        outgoing {
            capability("$group:${base.archivesName.get()}:$version")
            capability("$group:cdi-impl-spring-boot-starter:$version")
        }
    }
    runtimeElements {
        outgoing {
            capability("$group:${base.archivesName.get()}:$version")
            capability("$group:cdi-impl-spring-boot-starter:$version")
        }
    }
}